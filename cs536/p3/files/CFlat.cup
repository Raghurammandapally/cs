/**********************************************************************
 Java CUP specification for a parser for C Flat programs
 **********************************************************************/

import java_cup.runtime.*;
import java.util.*;

/* The code below redefines method syntax_error to give better error messages
 * than just "Syntax error"
 */
parser code {:

public void syntax_error(Symbol currToken) {
    if (currToken.value == null) {
        ErrMsg.fatal(0,0, "Syntax error at end of file");
    }
    else {
        ErrMsg.fatal(((TokenVal)currToken.value).linenum,
                     ((TokenVal)currToken.value).charnum,
                     "Syntax error");
    }
    System.exit(-1);
}
:};


/* Terminals (tokens returned by the scanner) */
terminal                INT;
terminal                BOOL;
terminal                VOID;
//https://piazza.com/class/id543c6knik6h7?cid=95
terminal TokenVal       TRUE;
terminal TokenVal       FALSE;
terminal                STRUCT;
terminal                CIN;
terminal                COUT;
terminal                IF;
terminal                ELSE;
terminal                WHILE;
terminal                RETURN;
terminal IdTokenVal     ID;
terminal IntLitTokenVal INTLITERAL;
terminal StrLitTokenVal STRINGLITERAL;
terminal                LCURLY;
terminal                RCURLY;
terminal                LPAREN;
terminal                RPAREN;
terminal                SEMICOLON;
terminal                COMMA;
terminal                DOT;
terminal                WRITE;
terminal                READ;
terminal                PLUSPLUS;
terminal                MINUSMINUS;
terminal                PLUS;
terminal                MINUS;
terminal                TIMES;
terminal                DIVIDE;
terminal                NOT;
terminal                AND;
terminal                OR;
terminal                EQUALS;
terminal                NOTEQUALS;
terminal                LESS;
terminal                GREATER;
terminal                LESSEQ;
terminal                GREATEREQ;
terminal                ASSIGN;


/* Nonterminals
 *
 * NOTE: You will need to add more nonterminals to this list as you
 *       add productions to the grammar below.
 */
non terminal ProgramNode      program;
non terminal LinkedList       declList;
non terminal DeclNode         decl;
non terminal VarDeclNode      varDecl;
non terminal TypeNode         type;
non terminal IdNode           id;
non terminal StructDeclNode   structDecl;
non terminal FnDeclNode       fnDecl;
non terminal LinkedList       formals;
non terminal FnBodyNode       fnBody;
non terminal LinkedList       stmtList;
non terminal LinkedList       formalsList;
non terminal FormalDeclNode   formalDecl;
non terminal StmtNode         stmt;
non terminal AssignNode       assignExp;
non terminal ExpNode          loc;
non terminal CallExpNode      fncall;
non terminal LinkedList       actualList;
non terminal ExpNode          exp;
non terminal ExpNode          term;


 
 
/* NOTE: Add precedence and associativity declarations here */
precedence left DOT, PLUS, MINUS, TIMES, DIVIDE, AND, OR;
precedence right ASSIGN;
precedence nonassoc LESS, GREATER, LESSEQ, GREATEREQ, EQUALS, NOTEQUALS;


start with program;


/* Grammar with actions
 *
 * NOTE: add more grammar rules below
 */
program         ::= declList: d
                {: RESULT = new ProgramNode(new DeclListNode(d));
                :}
                ;

declList        ::= declList:dl decl:d
                {: dl.addLast(d);
                   RESULT = dl;
                :}
                | /* epsilon */
                {: RESULT = new LinkedList<DeclNode>();
                :}
                ;

decl            ::= varDecl:v
                {: RESULT = v;
                :}
                | structDecl:s
                {: RESULT = s;
                :}
                | fnDecl:f
                {: RESULT = f;
                :}
                ;

//**********************************************
// varDeclList????
//**********************************************

varDecl         ::= type:t id:i SEMICOLON
                {: RESULT = new VarDeclNode(t, i, VarDeclNode.NOT_STRUCT);
                :}
                ;

fnDecl          ::= type:t id:i formals:f fnBody:fb
                {: RESULT = new FnDeclNode(t, i, new FormalsListNode(f), fb);
                :}
                ;

structDecl      ::= STRUCT id:i LCURLY declList:d RCURLY SEMICOLON
                {: RESULT = new StructDeclNode(i, new DeclListNode(d));
                :}
                ;

//**********************************************
// structBody????
//**********************************************


formals         ::= LPAREN RPAREN
                {: RESULT = new LinkedList<FormalDeclNode>();
                :}
                | LPAREN formalsList:f RPAREN
                {: RESULT = f;
                :}
                ;

formalsList     ::= formalDecl:f
                {: RESULT = new LinkedList<FormalDeclNode>();
                   RESULT.addLast(f);
                :}
                | formalDecl:f COMMA formalsList:fl
                {: fl.addLast(f);
                   RESULT = fl;
                :}
                ;

formalDecl      ::= type:t id:i
                {: RESULT = new FormalDeclNode(t, i);
                :}
                ;

fnBody          ::= LCURLY declList:d stmtList:s RCURLY
                {: RESULT = new FnBodyNode(new DeclListNode(d), new StmtListNode(s));
                :}
                ;

stmtList        ::= stmtList:sl stmt:s
                {: sl.addLast(s);
                   RESULT = sl;
                :}
                | /* epsilon */
                {: RESULT = new LinkedList<StmtNode>();
                :}

                ;

stmt            ::= assignExp:a SEMICOLON
                {: RESULT = new AssignStmtNode(a);
                :}
                | loc:l PLUSPLUS SEMICOLON
                {: RESULT = new PostIncStmtNode(l);
                :}
                | loc:l MINUSMINUS SEMICOLON
                {: RESULT = new PostDecStmtNode(l);
                :}
                | CIN READ loc:l SEMICOLON
                {: RESULT = new ReadStmtNode(l);
                :}
                | COUT WRITE exp:e SEMICOLON
                {: RESULT = new WriteStmtNode(e);
                :}
                | IF LPAREN exp:e RPAREN LCURLY declList:d stmtList:s RCURLY
                {: RESULT = new IfStmtNode(e, new DeclListNode(d), new StmtListNode(s));
                :}
                | IF LPAREN exp:e RPAREN LCURLY declList:d stmtList:s RCURLY ELSE LCURLY declList:d2 stmtList:s2 RCURLY
                {: RESULT = new IfElseStmtNode(e, new DeclListNode(d), new StmtListNode(s), new DeclListNode(d2), new StmtListNode(s2));
                :}
                | WHILE LPAREN exp:e RPAREN LCURLY declList:d stmtList:s RCURLY
                {: RESULT = new WhileStmtNode(e, new DeclListNode(d), new StmtListNode(s));
                :}
                | RETURN exp:e SEMICOLON
                {: RESULT = new ReturnStmtNode(e);
                :}
                | RETURN SEMICOLON
                {: RESULT = new ReturnStmtNode(null);
                :}
                | fncall:f SEMICOLON
                {: RESULT = new CallStmtNode(f);
                :}
                ;

assignExp       ::= loc:l ASSIGN exp:e
                {: RESULT = new AssignNode(l, e);
                :}
                ;

exp             ::= exp:e1 PLUS exp:e2
                {: RESULT = new PlusNode(e1, e2);
                :}
                | exp:e1 MINUS exp:e2
                {: RESULT = new MinusNode(e1, e2);
                :}
                | exp:e1 TIMES exp:e2
                {: RESULT = new TimesNode(e1, e2);
                :}
                | exp:e1 DIVIDE exp:e2
                {: RESULT = new DivideNode(e1, e2);
                :}
                | NOT exp:e 
                {: RESULT = new NotNode(e);
                :}
                | exp:e1 AND exp:e2
                {: RESULT = new AndNode(e1, e2);
                :}
                | exp:e1 OR exp:e2
                {: RESULT = new OrNode(e1, e2);
                :}
                | exp:e1 EQUALS exp:e2
                {: RESULT = new EqualsNode(e1, e2);
                :}
                | exp:e1 NOTEQUALS exp:e2
                {: RESULT = new NotEqualsNode(e1, e2);
                :}
                | exp:e1 LESS exp:e2
                {: RESULT = new LessNode(e1, e2);
                :}
                | exp:e1 GREATER exp:e2
                {: RESULT = new GreaterNode(e1, e2);
                :}
                | exp:e1 LESSEQ exp:e2
                {: RESULT = new LessEqNode(e1, e2);
                :}
                | exp:e1 GREATEREQ exp:e2
                {: RESULT = new GreaterEqNode(e1, e2);
                :}
                | MINUS term:t 
                {: RESULT = new UnaryMinusNode(t);
                :}
                | term:t
                {: RESULT = t; 
                :}
                ;

term            ::= loc:l
                {: RESULT = l;
                :}
                | INTLITERAL:i
                {: RESULT = new IntLitNode(i.linenum, i.charnum, i.intVal);
                :}
                | STRINGLITERAL:s
                {: RESULT = new StringLitNode(s.linenum, s.charnum, s.strVal);
                :}
                | TRUE:t
                {: RESULT = new TrueNode(t.linenum, t.charnum);
                :}
                | FALSE:f
                {: RESULT = new FalseNode(f.linenum, f.charnum);
                :}
                | LPAREN exp:e RPAREN
                {: RESULT = e;
                :}
                | fncall:f
                {: RESULT = f;
                :}
                ;

fncall          ::= id:i LPAREN RPAREN
                {: RESULT = new CallExpNode(i, null);
                :}
                | id:i LPAREN actualList:l RPAREN
                {: RESULT = new CallExpNode(i, new ExpListNode(l));
                :}
                ;

actualList      ::= exp:e
                {: RESULT = new LinkedList<ExpNode>();
                   RESULT.add(e);
                :}
                | actualList:l COMMA exp:e
                {:
                   l.addLast(e);
                   RESULT = l;
                :}
                ;

type            ::= VOID
                {: RESULT = new VoidNode();
                :}
                | BOOL
                {: RESULT = new BoolNode();
                :}
                | INT
                {: RESULT = new IntNode();
                :}
                ;

loc             ::= id:i
                {: RESULT = new DotAccessExpNode(null, i);
                :}
                | loc:l DOT id:i
                {: RESULT = new DotAccessExpNode(l, i);
                :}
                ;

id              ::= ID:i
                {: RESULT = new IdNode(i.linenum, i.charnum, i.idVal);
                :}
                ;

